plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.3'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'com.google.cloud.tools.jib' version "3.4.5"
}

group = 'kr.bi'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

jib {
    from {
        image = "eclipse-temurin:21-jre"
        platforms {
            platform {
                architecture = "arm64"
                os = "linux"
            }
        }
    }
    to {
        image = "ghcr.io/bi-2025-summer-team2/greenmate-be:latest"
        auth {
            username = System.getenv("DOCKER_USERNAME") ?: ""
            password = System.getenv("DOCKER_PASSWORD") ?: ""
        }
    }
    container {
        environment = ["TZ" : "Asia/Seoul"]
    }
}

dependencies {
    // Spring
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    // Secret & Config
    implementation(platform 'io.awspring.cloud:spring-cloud-aws-dependencies:3.3.0')
    implementation 'io.awspring.cloud:spring-cloud-aws-starter-parameter-store:3.3.0'
    implementation 'org.springframework.cloud:spring-cloud-starter-bootstrap:4.2.1'

    // Infrastructure
    implementation 'com.oracle.database.jdbc:ojdbc11:23.8.0.25.04'
    implementation 'org.redisson:redisson-spring-boot-starter:3.47.0'
    implementation 'io.awspring.cloud:spring-cloud-aws-starter-s3:3.2.1'  // 3.3버전에 이슈가 있어서 올리면 안됨

    // Documentation
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.8'

    // Utilities
    compileOnly 'org.projectlombok:lombok:1.18.38'
    annotationProcessor 'org.projectlombok:lombok:1.18.38'

    // Test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
    useJUnitPlatform()
}
